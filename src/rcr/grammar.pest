semi = _{ ";" }

int = @{ ('0'..'9')+ ~ !ID_CONTINUE }

reserved = @{
    "inc"
  | "dec"
  | "read"
  | "write"
  | "goto"
  | "fn"
  | "mut"
  | "each"
  | "let"
  | "while"
  | "exitable"
  | "exit"
}

name = @{ !reserved ~ (ID_START ~ ID_CONTINUE*) }

escape_seq = @{ "\\" ~ ("\\" | "\"" | "n" | "r") }
char_dq    = @{ (!("\\" | "\"" | NEWLINE) ~ ANY) | escape_seq }

target_name    =  { name }
target_array   =  { "[" ~ (target*) ~ "]" }
target_lit_int =  { int }
target_lit_str = @{ "\"" ~ char_dq* ~ "\"" }
target_expr    =  { "(" ~ stmt_list_no_semi ~ ")" }
target_inner   =  {
    target_name
  | target_array
  | target_lit_int
  | target_lit_str
  | target_expr
}
target         =  { target_inner ~ ("." ~ int)? }

fn_name_inner = @{ name | "inc" | "dec" | "read" | "write" | "goto" }
fn_name       = ${ fn_name_inner ~ !ID_CONTINUE }

stmt_while     = { keyword_while ~ target ~ block }
stmt_each      = { keyword_each ~ name ~ keyword_in ~ target ~ block }
stmt_breakable = { keyword_breakable ~ target? ~ block }
stmt_break     = { keyword_break }
stmt_call      = { fn_name ~ target* }
stmt_let       = { keyword_let ~ let_bindable ~ let_init? }

let_bindable      = _{ let_bind_dest | let_bind_standard }
let_bind_standard =  { name ~ let_array_size? }
let_bind_dest     =  { "[" ~ let_element* ~ "]" }
let_element       =  { name | "_" }

let_array_size = _{ "[" ~ int? ~ "]" }
let_init       = _{ "=" ~ target }

stmt_needs_semi        = { stmt_break | stmt_call | stmt_let }
stmt_no_mandatory_semi = { stmt_while | stmt_each | stmt_breakable }

stmt_list_semi    = {
    ((stmt_needs_semi ~ semi) | (stmt_no_mandatory_semi ~ semi?))*
}
stmt_list_no_semi = {
    ((stmt_needs_semi ~ semi) | (stmt_no_mandatory_semi ~ semi?))* ~ (stmt_needs_semi | stmt_no_mandatory_semi)
}

block = _{ "{" ~ stmt_list_semi ~ "}" }

fn_arg    =  { keyword_mut? ~ target ~ ("=" ~ target)? }
fn_args   = _{
    "(" ~ ")"
  | "(" ~ fn_arg ~ ","? ~ ")"
  | "(" ~ fn_arg ~ ("," ~ fn_arg)* ~ ","? ~ ")"
}
fn_return = _{ "->" ~ target }

fn = { keyword_fn ~ name ~ fn_args ~ fn_return? ~ block }

main = _{ SOI ~ fn* ~ EOI }

keyword_mut       = @{ "mut" ~ !ID_CONTINUE }
keyword_let       = @{ "let" ~ !ID_CONTINUE }
keyword_fn        = @{ "fn" ~ !ID_CONTINUE }
keyword_while     = @{ "while" ~ !ID_CONTINUE }
keyword_each      = @{ "each" ~ !ID_CONTINUE }
keyword_breakable = @{ "breakable" ~ !ID_CONTINUE }
keyword_break     = @{ "break" ~ !ID_CONTINUE }
keyword_in        = @{ "in" ~ !ID_CONTINUE }

WHITESPACE = _{ " " | "\t" | NEWLINE }

COMMENT = _{
    ("//" ~ ((!NEWLINE ~ ANY)+))
  | ("/*" ~ (!"*/" ~ ANY)* ~ "*/")
}
